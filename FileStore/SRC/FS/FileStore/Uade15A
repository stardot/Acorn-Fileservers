 OPT UADE15 ; > Uade15A
 TTL File server file UADE15A

; FSREPORT command tells authorised user only of last internal error.

Repmes ROUT
 [ Lang = English
   = "Last error was "
 ]
 [ Lang = Italian
   = "Ultimo errore era "
 ]
10 = "XX"
   = " X="
20 = "XX"
   = " Y="
30 = "XX"
 [ Lang = English
   = " at "
 ]
 [ Lang = Italian
   = " a "
 ]
40 = "XX"
50 = "XX"
   = " SIN="
60 = "XX"
70 = "XX"
80 = "XX"
   = CR,&80
Repend

Reptab 
 = #10-Repmes
 = #20-Repmes
 = #30-Repmes
 = #50-Repmes
 = #40-Repmes
 = #60-Repmes
 = #70-Repmes
 = #80-Repmes

Report ROUT
 JSR Spaces ;**1/6/88** look for arguments
 ANDIM &DF
 CMPIM "R"
 BNE #20
 BIT Cmndsw ;**1/6/88** must be maint mode 1 for reset
 BMI #00 ;**7/7/88** or user mode
 JSR Getusr ;**1/6/88** and privileged user
 BNE #20
 LDYIM Utpriv
 LDAIY Ustptr
 ANDIM Systpv
 BEQ #20
00 LDXIM IEaddr
10 LDAIM 0
 PHX
 JSR OSwrad ;**1/6/88** Reset reports to zero
 PLX
 INX
 CPXIM IEaddr+8
 BCC #10

20 LDXIM 0
30 LDAAX Repmes ;copy base message
 STAAX Midtx
 INX       
 CMPIM &80 ;Terminator?
 BNE #30

 LDXIM IEaddr
40 JSR OSrdad ;read next byte of error logout
 PHX
 TAY
 LDAAX Reptab-IEaddr ;msg offset
 TAX
 TYA
 JSR Wrterr ;place hex in msg
 PLX
 INX
 CPXIM IEaddr+8
 BCC #40 ;continue till all done
 
 LDAIM Repend-Repmes ;get msg length

Datour
 CLC
 ADCIM Txhdr
 LDXIM CCinf ;fudge up zero rc reply with msg
 STX Ccode
 JSR Replyc ;and go send reply to user
 JMP Comrts

; FSMAXDRIVE <n>  command sets max number of drives
; FSMAXUSER <n>   command sets max number of users
; FSSTN <n>       command sets ECOnet station id
; All commands allowed only in maintenance mode

RDMAXD ROUT
 LDXIM MDaddr ;eventual store address
 LDAIM 6+1 ;highest drive+1 (Winnies 0-3 + floppies 4,5)
 BRA #10

RDMAXU
 LDXIM MUaddr ;eventual store address
 LDAIM Maxuse+1 ;max user number+1
 BRA #10

SETSTN
 LDXIM 0 ;marker
 LDAIM &FF ;first invalid value
10 STX Cozero ;save parm offset
 STA Cozero+1 ;and max
 JSR Spaces
 LDXIM 0 ;running total
20 CMPIM "0"
 BCC #50
 CMPIM "9"+1
 BCS #50
 ANDIM &F ;digit value
 STA Cozero+2
 TXA ;get sum to date
 CMPIM 26 ;lowest value that will overflow
 BCS #60
 ASLA
 STA Cozero+3
 ASLA
 ASLA
 ADC Cozero+3 ;*10 (C was zero)
 ADC Cozero+2 ;+new digit
 BCS #60 ;overflowed
 TAX
 INY
 LDAAY Midrx ;next char
 BRA #20
 
50 JSR Comend
 BNE #70
 LDA Cozero ;check type
 CMPIM MDaddr ;FSMAXDRIVE?  ** 29/09/86 **
 BNE #52
 INX ;add one if so
52 TXA
 BEQ #60 
 CMP Cozero+1 ;check against max value
 BCS #60
 LDX Cozero ;ok, write to non-volatile memory
 BEQ #80
 JSR OSwrad
55 LDAIM 0 ;send null reply for success
 JMP Datous

60 LDAIM Numerr
 JMP Error

70 JMP Comrts ;message already sent

80 STA OSwork+1
 LDAIM 1 ; write fileserver number
 STA OSwork
 LDXIM :LSB:OSwork
 LDYIM :MSB:OSwork
 LDAIM &13 ; Station information
 JSR OSword ; write fileserver station
 BRA #55

; FSMODE <mode>  changes mode between maintenance and user modes.
; A system-privileged user can issue FSMODE M to terminate
; user-mode running and enter maintenance mode from his station.
; A user in maintenance mode can issue FSMODE U to terminate
; maintenance mode running and re-enter user mode.
; The form FSMODE alone is allowed from any mode, and reports
; the current running mode in text to the user.

FSMODE ROUT
 JSR Spaces ;Find arg
 ANDIM &DF ;Uppercase if alpha
 CMPIM "M"
 BEQ #10
 CMPIM "U"
 BEQ #20
 CMPIM CR
 BEQ #40
 LDAIM Woterr ;Error if neither
05 JMP Error

10 BIT Cmndsw ;To Maintenance mode 1, must be in user mode
 BMI #32
 JSR Getuse ;Get client station info and set MCNUMB
 BNE #30
 LDYIM Utpriv
 LDAIY Ustptr
 ANDIM Systpv ;Check system privileged
 BEQ #15 ;Error if not
 LDA MCnumb ;Preserve MCNUMB
 PHA
 LDA MCnumb+1
 PHA
 JSR #80 ;Take down world
 PLA ;Restore MCNUMB
 STA MCnumb+1
 PLA
 STA MCnumb
 LDAIM &C0 ;Set maintenance mode 1
 STA Cmndsw
 JMP Strtf2 ;Partial restart, MCNUMB preset

15 LDAIM Aterrd ;Insufficient privilege
 BRA #05

20 BIT Cmndsw ;To user mode, must be in maintenance mode
 BPL #32
 BVC #22
 JSR FSmody ;If maintenance mode 1 check locked station
 BNE #32
22 LDAIM 128 ;...and with drive door shut
 LDXIM 0
 JSR OSbyte ;read door switch
 ANDIM 2
 BNE #25 ;Door open, bounce
 JSR #80 ;Take down world
 STZ Cmndsw ;Set user mode (for neatness)
 LDAIM 0 ;Record door open and ...
 JMP Strtf1 ;Full FileStore restart

25 LDAIM Doorer ;Say "Door open"
 BRA #05

30 JMP Comrts

32 JMP Badmod ;Say bad in this mode

40 LDXIM 0 ;build mode message alone
 JSR #70
 LDAIM &80 ;add CCinf terminator
 STAAX Midtx
 INX
 TXA
 JMP Datour ;send msg as info message

 [ Lang = English
65 = "User mode",CR
66 = "Maintenance mode 0",CR
67 = "Maintenance mode 1",CR
 ]
 [ Lang = Italian
65 = "Modo Utente",CR
66 = "Modo Mantenzione 0",CR
67 = "Modo Mantenzione 1",CR
 ]

Setmod
70 LDYIM 0 ;describe mode (always)
 BIT Cmndsw
 BPL #75
 LDYIM #66-#65
 BVC #75
 LDYIM #67-#65
75 LDAAY #65
 STAAX Midtx ;copy mode message
 INY
 INX
 CMPIM CR
 BNE #75
 RTS

80 LDAIM 0 ;space saver
 JSR Rcode ;reply ok now, world vanishing
 LDAIM 137
 LDXIM 1
 JSR OSbyte ;turn on led

 LDAIM &34 ;cancel RECEIVE control block
 LDX RXtab
 JSR OSbyte

 BIT Cmndsw ;no more shutdown if maintenance mode
 BMI #90
 LDAIM 10 ;but if user mode...
 JSR SETRTN
 JSR USRMAN ;*** LOGOFF ALL USERS **
 PHA
 JSR Flusys ;ensure discs
 PLA
 BEQ #90 ;br if ok
 JSR Interr ;else report problem
90 RTS

FSmody
 LDY Qptr ;Check this is locked station
 LDAAY CBstid
 CMP MCnumb
 BNE #85
 LDAAY CBstid+1
 CMP MCnumb+1
85 RTS ;Exit Z set appropriately

; FSPROT <ON|OFF>  sets ECOnet protection on or off.  May be
; issued from maintenance mode 1 only.

FSPROT ROUT
 JSR Spaces ;skip spaces
 ANDIM &DF
 CMPIM "O"
 BNE #50 ;Error
 INY
 LDAAY Midrx ;next char
 ANDIM &DF
 CMPIM "N"
 BEQ #10
 CMPIM "F"
 BNE #50 ;Error unless N or F
 INY
 LDAAY Midrx ;next char
 ANDIM &DF
 CMPIM "F"
 BNE #50
 LDXIM 0 ;protection-off mask
00 INY
 JSR Comend
 BNE #60
 TXA ;mask to A 
 JSR DoProt ;set mask
 LDAIM 0 ;success
 JMP Datous

10 LDXIM Mask ;protection-on mask
 BRA #00

50 LDAIM Woterr
 JMP Error

60 JMP Comrts ;message already send

; FSUSER <userid> sets into CMOS RAM the userid to replace 
; the fallback id as that to be used to log on from 
; maintenance mode 0 into maintenance mode 1.

FSUSER ROUT
 JSR Spaces ;Find argument string
 JSR Buftxt ;Read into TXTBUF
 BNE #40
 JSR Comend
 BNE #40

 LDXIM 0 ;check string and find length
00 LDAAX Txtbuf
 CMPIM CR
 BEQ #05 
 JSR ISDCHA ;Check syntax
 BCS #50
 INX
 BNE #00 ;stop when found
05 TXA ;check some chars present
 BEQ #50
 CPXIM FSusie-FSusid-1 ;Check max length
 BCS #50 ;(less checksum byte)

 STZ Tempa ;clear checksum
 LDXIM FSusid
10 LDAAX Txtbuf-FSusid ;next char
 PHA
 JSR OSwrad ;write to CMOS RAM
 PLA
 PHA
 CLC
 ADC Tempa ;accumulate checksum
 BCC #20 ;with end-round carry
 INCA
20 STA Tempa
 INX
 PLA
 CMPIM CR ;propagate CR to end
 BNE #30
 STAAX Txtbuf-FSusid
30 CPXIM FSusie-1
 BCC #10
 LDA Tempa ;then add checksum
 JSR OSwrad

35 LDAIM 0
 JMP Datous ;end with zero return code

40 JMP Comrts

50 LDAIM Aterrg
60 JMP Error

; FSNAMEDISC <n> <title> renames a disc.  Maintenance mode 1 only.
; Reads both descriptor sectors for the disc and replaces the name
; with that specified.

NMDISC JSR NMdisr ;**22/3/88** Read arguments
 JSR Rddata ;Read first descriptor sector
70 BNE #50 
 LDYIM MPsznm
80 LDAAY Txtbuf-MPsznm ;Copy in new disc name
 STAIY Mpcypt
 INY
 CPYIM MPsznm+Dnamln
 BCC #80
 LDAIM &0A ;Write code
 STA Dsccb+Cmd
 JSR Dsccmd ;Write back sector
 BNE #60
 LDA RBda ;First or second done?
 BNE #35 ;Second done
 INC RBda ;Set second now
 LDA MPdrve
 JSR RDstwx
 BRA #70

; Subroutine, read disc number followed by disc name.
; Exit: A=Disc number, TXTBUF=disc name.
; Syntax errors pop link and take error actions.

NMDISR ROUT
 JSR Spaces ;**18/2/88** Find disc number
 CMPIM "0"
 BCC #85
 CMPIM "6"
 BCS #85
 ANDIM 7
 STA Mpdrve ;Set variables for RDDATA routine in MAPMAN
 INY ;(Maybe should make this a MAPMAN function)
 JSR Rdtitl ;Read disc name
 BNE #80
 LDXIM &FF
60 INX
 LDAAX Txtbuf ;Check and find end of name
 CMPIM "A"
 BCC #61
 CMPIM "Z"+1
 BCC #60
61 CMPIM "a"
 BCC #62
 CMPIM "z"+1
 BCC #60
62 CPXIM 0 ;first char?
 BEQ #63
 CMPIM "-"
 BEQ #60
 CMPIM "_"
 BEQ #60
 CMPIM "0"
 BCC #63
 CMPIM "9"+1
 BCC #60 
63 CMPIM CR
 BNE #90
 CPXIM 0
 BEQ #90
 CPXIM Dnamln+1
 BCS #90
 LDAIM Space
65 STAAX Txtbuf
 INX
 CPXIM Dnamln
 BCC #65
 LDA Mpdrve
 RTS

80 PLA ;**22/3/88** unstack link
 PLA
 JMP Comrts

85 PLA ;**4/4/88** unstack link
 PLA
 LDAIM Numerr
 JMP Error

90 PLA ;**22/3/88** unstack link
 PLA
 LDAIM Synerr
 JMP Error

 LNK UADE16
