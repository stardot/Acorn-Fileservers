 OPT UADE17 ;FILE > Uade17A
 TTL File server file UADE17A

;**********************
;* S E L E C T  D I R *
;**********************

SELDIR ROUT ;SELDIR

;ALL WORK DONE BY USERMAN AGAIN.

 JSR RDTITL
 BNE #10
 JSR STKUSE
 BNE #10

 JSR SBUFPT ;** 5/9/84 **
 LDYIM ARGF
 JSR STKHND ;Handles -> stack
 JSR SELHAN
 BNE #20 ;error in allocation

 LDAIM CCSDIR
00 STA CCODE
 LDAIM TXHDR+1 ;Message length
 JSR REPLYC
10 JMP COMRTS

20 JMP ERROR


SELHAN LDAIM 5 ;ROUTINE NUMBER
 JSR SETRTN
 JSR USRMAN ;*** SELECT DIR. **
 BNE #30 ;exit if fail

 LDYIM ARGB
 LDAIY NEWARG
 STA MIDTX ;New s.dir
 LDAIM 0
30 RTS

;******************************
;* S E L E C T  L I B R A R Y *
;******************************

;as for DIR but null filename is illegal
;if RETRIEVE ok then call USRMAN with LIB as CSD

SLIB JSR RDTITL
 BNE #10
 JSR STKUSE
 BNE #10
 JSR SBUFPT
 JSR SCOWPT ;Set pointer to COWORK on stack
 LDAIM 2 ;Dirman retrieve
 JSR SETRTN

 LDYIM ARGH
 LDAIM &C0
 STAIY NEWARG ;wild card flag

 JSR DIRMAN ;*** RETRIEVE **
 BNE #20

 JSR STKUSE
 BNE #10
 JSR SBUFPT
 JSR SCOWPT ;Set pointer to COWORK on stack

 LDYIM ARGF
 JSR STKHND ;handles -> stack

 LDYIM ARGH
 LDAIY NEWARG
 DEY
 STAIY NEWARG ;LIB->CSD
 
 JSR SELHAN ;** 5/9/84 **
 BNE #20 ;error in allocation

 LDAIM CCSLIB
 BRA #00


;*********************
;* S E T A C C E S S *
;*********************


SETACC ROUT

;1) PUT USER INFO. POINTER AND FILE TITLE
;POINTER ON STACK.
;2) INTERPRET STRING SENT FROM CLIENT INTO
;AN ACCESS BYTE AND STICK ON STACK.
;3) CALL SETATTRIBS IN DIRMAN
;4) REPLY TO CLIENT


 JSR BUFTXT ;Get file name
 BNE #40
 STY COWORK ;Store cmnd. line ptr.
 JSR STKUSE
 BNE #40

 JSR SBUFPT

 LDY COWORK ;Restore cmnd. line ptr.
 JSR SPACES

 STZ COZERO ;Partial result
 DEY
10 INY
 LDXIM 2 ;Offset into access tables
 JSR CMPACC ;Compare letter at Y with access tables
 BEQ #10 ;Access letter found
 CMPIM "/" ;Owner access delimiter
 BEQ #20
 JSR SPACER ;skip trailing spaces ** 25/5/83 **
 CMPIM CR
 BNE #50 ;Invalid character => error
 DEY ;Point to CR so will exit after rotating COZERO

20 ASL COZERO ;Rotate access bits to owner position
 ASL COZERO

30 INY ;Increment past '/' delimiter
 LDXIM 1
 JSR CMPACC
 BEQ #30
 JSR SPACER ;skip trailing spaces ** 25/5/83 **
 CMPIM CR
 BNE #50 ;If here, CR has terminated string => set access

 LDYIM ARGF
 LDAIM &C0
 STAIY NEWARG ;wild cards flag
 LDAIM 4 
 INY ;Set arg to SETATTRIBS => SETACCESS
 STAIY NEWARG
 INY
 LDA COZERO ;Restore access byte
 STAIY NEWARG ;SET ACCESS BITS
 LDAIM 4 ;SETACC ROUTINE NO.
 JSR SETRTN
 JSR DIRMAN ;*** SET ACCESS **
 JSR RCODE ;SEND REPLY
40 JMP COMRTS ;FINISH

50 LDAIM SAERRA
 JMP ERROR


OWNTAB
 = "R"
 = "W"
 = "L"

POWNTB = 1 ;OWNER READ
 = 2 ;OWNER WRITE
 = 4

CMPACC ROUT
 LDAAY MIDRX
 EORAX OWNTAB
 ANDIM &DF ;Compare with cases forced
 BEQ #10
 DEX
 BPL CMPACC
 LDAAY MIDRX ;Exit, A = char, Z unset
 RTS

10 LDAAX POWNTB
 TSB COZERO ;Update partial result
 LDXIM 0 ;Set Z flag; note A corrupted
 RTS



;******************
;* N E W  U S E R *
;******************


NEWUSE ROUT

;ALL WORK DONE IN AUTMAN, ROUTINE NO. 1

 JSR RDTITL ;Get user name
 BNE #10
 JSR STKUSE ;Stack caller's user info
 BNE #10
 JSR SBUFPT ;Stack new user ptr.

 LDAIM 1
DOUSE JSR SETRTN ;SET ROUTINE NUMBER
 JSR AUTMAN ;DO NEW USER
 JSR RCODE ;SEND RETURN CODE
RUEXIT  ;Shared with REMUSE
SPWEXT  ;Shared with SETPW
10 JMP COMRTS ;FINISH



;************************
;* R E M O V E  U S E R *
;************************

REMUSE ROUT

;REMARKABLY SIMILAR TO NEWUSER

 JSR RDTITL
 BNE RUEXIT
 JSR STKUSE
 BNE RUEXIT
 JSR SBUFPT

 LDAIM 4
 BRA DOUSE ;Always jump


;**************************
;* S E T  P A S S W O R D *
;**************************


SETPW ROUT

;GET USER INFO AND CALL ROUTINE IN AUTMAN
;TO DO WORK.

 JSR BUFTXT ;Buffer old pw
 BNE SPWEXT
 INX ;Ptr. to next free spot in TXTBUF
 JSR BTXTA ;Buffer new pw
 BNE SPWEXT
 JSR COMEND
 BNE SPWEXT
 JSR STKUSE
 BNE SPWEXT
 JSR SBUFPT

 LDYIM UTPRIV ; check low-privilege *** 25/5/86 ***
 LDAIY USTPTR ; *** 25/5/86 ***
 ANDIM LOPRIV ; *** 25/5/86 ***
 BNE #10 ; br if password not allowed *** 25/5/86 ***

 LDAIM 3 ;ROUTINE NUMBER
 BRA DOUSE ;Always jump

10 LDAIM ATERRD ; Insufficient privilege *** 25/5/86 ***
 JMP ERROR ; *** 25/5/86 ***


;**********************
;* C R E A T E  D I R *
;**********************


CDIRFN ROUT
 JSR Stkuse ; Check logged-on first
 BNE #20 ; *** 31/10/86 ***
 LDY BPTR
 INY
 JSR RDTITL
 BNE #20
 LDY BPTR
 LDAAY MIDRX
 BEQ #30 ;zero is a silly number
 CMPIM (:MSB:MAXDIR)+1
 BCS #30
 BRA #10 ;check for overflow

CDIR   ;CDIR

;PASS USER INFO AND DIRECTORY NAME
;TO DIRMAN. THEN CLEAN UP IF NECCESSARY.

 JSR RDTITL
 BNE #20
 LDAIM 2 ;size of directory

10 CMP CACHSZ+1 ;** 31/1/85
 BCS #30
 LDYIM ARGF
 STAIY NEWARG
 INY
 LDAIM &80 ;** 11/10/84 **
 STAIY NEWARG ;wild card flag
 JSR STKUSE
 BNE #20

 JSR SBUFPT
 LDAIM 5 ;CREATE DIR. FUNCTION CODE
 JSR SETRTN
 JSR DIRMAN ;*** DO DIR. CREATE **
 BNE #15
 JSR OBJCLR ;CLEAR OLD OBJECT
15 JSR RCODE ;store RC

SPEXIT  ;Shared with SETPRIV
20 JMP COMRTS

30 LDAIM DRERRP
 JMP ERROR


;******************
;* S E T  P R I V *
;******************


STPRIV ROUT

;MOST WORK DONE BY AUTMAN

 JSR BUFTXT ;MOVE USER ID.
 BNE SPEXIT
 STY COWORK
 JSR STKUSE
 BNE SPEXIT
 JSR SBUFPT

 LDY COWORK
 JSR SPACES
 ANDIM &DF ;Force upper case
 CMPIM "S" ;SYSTEM USER ???
 BEQ #10
 CMPIM "L" ;ow-privilege user? *** 25/5/86 ***
 BEQ #50 ; *** 25/5/86 ***
 CMPIM CR ;NON-SYSTEM USER ??
 BNE #30
 LDAIM 0 ;NON-SYST. USER
 BRA #20

10 INY
 JSR COMEND
 BNE SPEXIT
 LDAIM 1 ;SYSTEM USER !
20 LDYIM ARGF
 STAIY NEWARG ;Set user's priv. flag
 LDAIM 5 ;SET PRIV.
 JMP DOUSE

30 LDAIM SPERRA
40 JMP ERROR

50 INY ; *** 25/5/86 ***
 JSR COMEND ; *** 25/5/86 ***
 BNE SPEXIT ; *** 25/5/86 ***
 LDAIM 2 ;Low-privilege user *** 25/5/86 ***
 BRA #20 ; *** 25/5/86 ***

;*************
;* D I S C S *
;*************

DISCS ROUT

;Provide list of discs currently on system.

;User supplies entry (drive) number, and
;number of drives. For each drive he is interested in,
;call MAPMAN once to get disc number
;and again to get disc name.

 STZ COZERO ;No. of drives found
 LDA BBUF
 CLC
 ADCIM TXHDR+1 ;Result buffer
 STA COZERO+1
 LDA BBUF+1
 ADCIM 0
 STA COZERO+2
10 LDX BPTR
 LDAAX MIDRX
 CMP DRIVES
 BCS #30 ;User drive >= drives

 STAI COZERO+1 ;Store probable drive number
 LDYIM ARGB
 STAIY NEWARG
 LDAIM 8 ;Drive -> disc no.
 JSR SETRTN
 JSR MAPMAN
 BNE #28 ;Abort
 LDAIM &B ;Disc no. -> name
 JSR SETRTN
 JSR MAPMAN
 BNE #40
 INC COZERO+1
 BNE #20
 INC COZERO+2
20 LDA COZERO+1
 STA MOVTO
 LDA COZERO+2
 STA MOVTO+1

 LDYIM ARGB
 LDAIY NEWARG ;Ptr. to name
 STA MOVFRM
 INY
 LDAIY NEWARG
 STA MOVFRM+1
 LDXIM DNAMLN
 JSR MOVE ;Move name to result
 CLC
 LDA COZERO+1
 ADCIM DNAMLN
 STA COZERO+1
 BCC #25
 INC COZERO+2
25 INC COZERO ;No. of drives dealt with
28 LDX BPTR ;**14/06/86**
 INCAX MIDRX ;Next drive to do
 LDA Cozero ;**1/5/87** loop until requested drives done
 CMPAX Midrx+1 ;**1/5/87**
 BCC #10 ;**1/5/87**

30 LDYIM ARGB
 LDA COZERO ;Set args to LSTPRY
 STAIY NEWARG
 INY
 LDA COZERO+1
 STAIY NEWARG
 INY
 LDA COZERO+2
 STAIY NEWARG
 LDAIM CCDSCS
 JSR LSTRPY ;Reply to client
35 JMP COMRTS

40 JSR EXTERR
 BRA #35

;*************
;* U S E R S *
;*************

CPUSRS ROUT

 JSR STKLST

 JSR GETUSR ;Get current user (16/1/86)
 BNE #15 ;***31/10/86***
 LDYIM UTPRIV
 LDAIY USTPTR
 ANDIM SYSTPV ;Stack privilege for USRMAN
05 LDYIM ARGF
 STAIY NEWARG
 LDX BPTR
 LDAAX RXBUF+1 ;**20/5/87** check function code
 EORIM (FNusr2-FNtab)/2 ;**20/5/87** zero if job/task nos
 INY
 STAIY NEWARG

 LDAIM 7
 JSR SETRTN
 JSR USRMAN
 BNE #10
 LDAIM CCUSRS
 JSR LSTRPY
 JMP COMRTS

10 JMP ERROR
  
15 LDAIM 0 ;not logged on, hide priv users
 BRA #05

;***********
;* D A T E *
;***********


CPSETD ROUT ;Set the date ** 20/9/83 **
 JSR Getuse ;***Check caller logged on***
 BNE #20
 LDYIM Utpriv ;***and system priv***
 LDAIY Ustptr
 ANDIM Systpv
 BEQ #30 ;***31/10/86***
 LDA DATE
 PHA
 LDA DATE+1
 PHA ;SAVE DATE IN CASE WE NEED IT!
 LDY BPTR ;check flag
 LDAAY MIDRX
 STA DATE
 LDAAY MIDRX+1
 STA DATE+1
 JSR CHKDTE
 BNE #10
 LDY BPTR
 LDAAY MIDRX+2
 CMPIM 24
 BCS #10
 STA HRS
 LDAAY MIDRX+3
 CMPIM 60
 BCS #10
 STA MINS
 LDAAY MIDRX+4
 CMPIM 60
 BCS #10
 STA SECS
 JSR SETDTE ;Set date & time
 PLA
 PLA ;REMOVE SAVED DATE
 LDAIM TXHDR ;Rx header only
 BRA DATOUT

10 PLA
 STA DATE+1
 PLA
 STA DATE
 LDAIM DTERR
 JSR RCODE
20 JMP COMRTS

30 LDAIM ATerrd  ;Insufficient privilege *** 31/10/86 ***
 JMP Error ;*** 31/10/86 ***

CPDATE ROUT

;No user check since doesn't involve
;files.

 JSR GETDTE ;Get the date & time
 LDA DATE ;read date
 STA MIDTX
 LDA DATE+1
 STA MIDTX+1
 LDA HRS
 STA MIDTX+2
 LDA MINS
 STA MIDTX+3
 LDA SECS
 STA MIDTX+4 ;Time = zero if clock not present
 LDAIM TXHDR+5 ;Message length
 BRA Datout


;*****************
;* V E R S I O N *
;*****************

VERSN = "FileStore "
 = "0"+VERLA, ".", "0"+VERLB/10, "0"+VERLB-VERLB/10*10, CR, 6

CPVERN ROUT
 LDYIM 0
10 LDAAY VERSN
 STAAY MIDTX
 INY
 CMPIM 6 ;end of version number character
 BNE #10

 TYA
Datous ;Entry with text length in A
 CLC
 ADCIM TXHDR
Datout ;Entry with msg length in A
 STZ CCODE ;0 command code
 JSR REPLYC ;Message length in A here
Datoux ;Here to just exit
 JMP COMRTS

 LNK UADE18
